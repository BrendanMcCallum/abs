# Maintainer: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=spigot
pkgver=1.8.8
pkgrel=3
pkgdesc="High performance Minecraft server implementation"
arch=('any')
url="https://www.spigotmc.org/"
license=('LGPL')
depends=('java-runtime-headless' 'screen' 'sudo' 'fontconfig' 'bash')
optdepends=('tar: needed in order to create world backups')
makedepends=('java-environment' 'git')
provides=("minecraft-server" "bukkit" "craftbukkit=${pkgver%_*}")
conflicts=("bukkit" "craftbukkit" "spigot-patcher")
backup=("etc/conf.d/${pkgname}")
install=${pkgname}.install
source=("https://hub.spigotmc.org/jenkins/job/BuildTools/lastSuccessfulBuild/artifact/target/BuildTools.jar"
	"${pkgname}-backup.service"
	"${pkgname}-backup.timer"
	"${pkgname}.service"
	"${pkgname}.conf"
	"${pkgname}.sh")
noextract=("BuildTools.jar")
md5sums=('47f1ff7f1466c0328abfd93f40856961'
         'fd17202ba0bb7796439f0b2f6bc53be4'
         '774cd44ed90f6be51d368533cf74d7c2'
         'd9ca80f88da4145acf339458757cbd24'
         'abdc0e0fd9528bd5ae4155f45f67c857'
         '17c5f2c6784bc5f1cb237cfab63c9f9e')

build() {
	export MAVEN_OPTS="-Xmx2g -XX:MaxPermSize=1g"
	java -jar BuildTools.jar --rev ${pkgver}
}

package() {
	install -Dm644 spigot.conf              "${pkgdir}/etc/conf.d/spigot"
	install -Dm755 spigot.sh                "${pkgdir}/usr/bin/spigot"
	install -Dm644 spigot-${pkgver}.jar     "${pkgdir}/srv/spigot/spigot.jar"
	install -Dm644 spigot.service           "${pkgdir}/usr/lib/systemd/system/spigot.service"
	install -Dm644 spigot-backup.service    "${pkgdir}/usr/lib/systemd/system/spigot-backup.service"
	install -Dm644 spigot-backup.timer      "${pkgdir}/usr/lib/systemd/system/spigot-backup.timer"
}
