# Maintainer: Gordian Edenhofer <gordian.edenhofer@gmail.com>

# To adapt this script to your liking e.g. install a different game, additional mods etc. you may want to change
#     * build()-function
#     * variables here on top
#     * server.cfg file
#     * systemd files
#     * control-script and its config file

_game="ArmA3"
_steamcmd_root="/srv/steamcmd"
_server_root="/srv/steamcmd/${_game,,}"

# Prevent compression of the final package since it would take too long (sereausly!)
PKGEXT='.pkg.tar'

pkgname=${_game,,}-server
pkgver=latest
pkgrel=1
pkgdesc="Steam Command Line Tools"
arch=('i686' 'x86_64')
url="https://community.bistudio.com/wiki/Arma_3_Dedicated_Server"
license=('custom')
depends=('steamcmd')
optdepends=('tar: needed in order to create server backups')
install="${_game,,}.install"
backup=("etc/conf.d/${_game,,}d.conf")
source=("${_game,,}d.sh"
	"${_game,,}d-backup.service"
	"${_game,,}d-backup.timer"
	"${_game,,}d.service"
	"${_game,,}d.conf"
	"server.cfg")
md5sums=('dfa65a20189bf9470b0849826a9041c0'
         '9b10d6c9c6cfc631ca6b69b184e3a303'
         '65881fe56dcf7dcd4a8b412994beaa46'
         '2262cc63b8e3f0dde7cd50363d382541'
         '179d238b1ceaa580fdf524391a26d01f'
         '181bebe43a9319101cdcd4884a3b2751')

build() {
	# Copying steamcmd files
	cp -dpr --no-preserve=ownership ${_steamcmd_root}/linux32     ./
	cp -dpr --no-preserve=ownership ${_steamcmd_root}/package     ./
	cp -dpr --no-preserve=ownership ${_steamcmd_root}/public      ./
	cp -dpr --no-preserve=ownership ${_steamcmd_root}/steamcmd.sh ./
	cp -dpr --no-preserve=ownership ${_steamcmd_root}/steam.sh    ./

	# Alter the following line to install a different game
	# Refer to https://developer.valvesoftware.com/wiki/Steam_Application_IDs for the according Steam Application ID
	# For some games you need to specify valid steam login credentials, create a new steam account in this case!
	# For further reading check out https://developer.valvesoftware.com/wiki/SteamCMD#Linux
	steamcmd.sh "+login anonymous" "+force_install_dir ./${_game,,}" +app_update 233780 validate +quit

	# Uncomment and alter this line to install additional mods
	#steamcmd +login anonymous "+force_install_dir ./${_game,,}/<MOD_PATH>" +app_set_config 90 mod [MOD_NAME] +app_update 90 +quit
}

package() {
	# Install the server configuration file
	install -D -m 644 "${srcdir}/${_game,,}d.conf"           "${pkgdir}/etc/conf.d/${_game,,}d.conf"
	install -D -m 755 "${srcdir}/${_game,,}d.sh"             "${pkgdir}/usr/bin/${_game,,}d"
	install -D -m 644 "${srcdir}/${_game,,}d.service"        "${pkgdir}/usr/lib/systemd/system/${_game,,}d.service"
	install -D -m 644 "${srcdir}/${_game,,}d-backup.service" "${pkgdir}/usr/lib/systemd/system/${_game,,}d-backup.service"
	install -D -m 644 "${srcdir}/${_game,,}d-backup.timer"   "${pkgdir}/usr/lib/systemd/system/${_game,,}d-backup.timer"

	# Move the game files to ${_server_root}
	install -D -m 644 "${srcdir}/server.cfg" "${pkgdir}/${_server_root}/server.cfg"
	cp -dpr --no-preserve=ownership "${srcdir}/${_game,,}/." "${pkgdir}/${_server_root}"

	# Give the group write permissions and set user or group ID on execution
	chmod g+ws "${pkgdir}/${_server_root}"
}
